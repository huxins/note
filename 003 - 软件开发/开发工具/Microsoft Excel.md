# Microsoft Excel

[Microsoft Excel](https://support.microsoft.com/zh-cn/excel) 是 Microsoft 为使用 Windows 和 macOS 操作系统的电脑编写的一款电子表格软件。

## 一、函数

### 数据引用与定位

#### 获取引用信息

[`ROW`](https://support.microsoft.com/zh-cn/office/3a63b74a-c4d0-4093-b49a-e76eb49a6d8d) 函数返回引用的行号。

```sql
=ROW(A5)
-- Result: 5
```

[`ROWS`](https://support.microsoft.com/zh-cn/office/b592593e-3fc2-47f2-bec1-bda493811597) 函数返回引用中包含的行数。

```sql
=ROWS(C1:E4)
-- Result: 4
```

[`COLUMN`](https://support.microsoft.com/zh-cn/office/44e8c754-711c-4df3-9da4-47a55042554b) 函数返回引用的列号。

```sql
=COLUMN(C5)
-- Result: 3
```

[`COLUMNS`](https://support.microsoft.com/zh-cn/office/4e8e7b4e-e603-43e8-b177-956088fa48ca) 函数返回引用中包含的列数。

```sql
=COLUMNS(C1:E4)
-- Result: 3
```

#### 动态引用生成

[`ADDRESS`](https://support.microsoft.com/zh-cn/office/d0c26c0d-3991-446b-8de4-ab46431d4f89) 函数根据指定行号和列号，以文本形式返回工作表中的某个单元格的地址。

```sql
=ADDRESS(2, 3)
-- Result: $C$2
```

可以使用其他函数，如 [`ROW`](https://support.microsoft.com/zh-cn/office/3a63b74a-c4d0-4093-b49a-e76eb49a6d8d) 和 [`COLUMN`](https://support.microsoft.com/zh-cn/office/44e8c754-711c-4df3-9da4-47a55042554b) 函数为 [`ADDRESS`](https://support.microsoft.com/zh-cn/office/d0c26c0d-3991-446b-8de4-ab46431d4f89) 函数提供行号和列号参数。

```sql
=ADDRESS(ROW(), COLUMN())
```

[`INDIRECT`](https://support.microsoft.com/zh-cn/office/474b3a3a-8a26-4f44-b491-92b6306fa261) 函数返回由文本字符串指定的引用。

```sql
-- A1 样式引用
=INDIRECT("A1")

-- R1C1 样式引用
=INDIRECT("R1C1", FALSE)
```

可以使用其他函数，如 [`ADDRESS`](https://support.microsoft.com/zh-cn/office/d0c26c0d-3991-446b-8de4-ab46431d4f89) 函数为 [`INDIRECT`](https://support.microsoft.com/zh-cn/office/474b3a3a-8a26-4f44-b491-92b6306fa261) 函数提供参数。

[`OFFSET`](https://support.microsoft.com/zh-cn/office/c8de19ae-dd79-4b9b-a14e-b4d906d11b66) 函数从给定引用中，按偏移量返回引用。

```sql
OFFSET(reference, rows, cols, [height], [width])

# 在 D3 引用的基础上，加上偏移量
=OFFSET(D3, -2, -3)
-- Result: A1

# 在行中，倒叙引用指定列的数据
=OFFSET($D$34, -(COLUMNS($B$5:B5) - 1), 0)
```

[`INDEX`](https://support.microsoft.com/zh-cn/office/a5dcf0dd-996d-40a4-a822-b56b061328bd) 函数使用索引从引用或数组中选择值。

```sql
INDEX(reference, row_num, [column_num], [area_num])

# 在行中，倒叙引用指定列的数据
=INDEX($D:$D, ROW($D$34) - COLUMNS($B$5:B5) + 1)
```

#### 数据查找与引用

[`MATCH`](https://support.microsoft.com/zh-cn/office/e8dffd45-c762-47d6-bf89-533f4a37673a) 函数在引用或数组中查找值，返回该项在该区域中的相对位置。

- *match_type*：`1` 完全匹配或次小，`0` 完全匹配，`-1` 完全匹配或次大。

```sql
MATCH(lookup_value, lookup_array, [match_type])

# 区域 A1:A3 包含值 5、7 和 38，找出 7 是该区域中的第几个项目
=MATCH(7, A1:A3, 0)

# E 列为访客人数，C 列为日期，通过 MAXIFS 筛选指定日期最大的访客数，通过 MATCH 获取行号
=MATCH(MAXIFS(E:E, C:C, DATE(2023,6,1)), E:E, 0)

# 根据 MATCH 获取指定日期最大访客数的行号，配合 INDEX 函数，取出对应关键词数据
=INDEX(D:D, MATCH(MAXIFS(E:E, C:C, DATE(2023, 6, 1)), E:E, 0))
```

[`VLOOKUP`](https://support.microsoft.com/zh-cn/office/0bbc8083-26fe-4963-8ab8-93a18ad188a1) 函数在数组第一列中查找，然后在行之间移动以返回单元格的值。

- *range_lookup*：默认为 `TRUE` 近似匹配。`FALSE` 为完全匹配。

```sql
VLOOKUP(lookup_value, table_array, col_index_num, [range_lookup])

# 根据 H3 的值，在 B4:E13 的范围内查找相匹配的数据，选择第二列
=VLOOKUP(H3, B4:E13, 2, FALSE)
```

[`FILTER`](https://support.microsoft.com/zh-cn/office/f4f7cb66-82eb-4767-8f7c-4877ad80c759) 函数基于定义的条件筛选一系列数据。

```sql
=FILTER(array, include, [if_empty])

# 返回 C 列值等于 H2 的行
=FILTER(A5:D20, C5:C20=H2, "")
```

[`DGET`](https://exceljet.net/functions/dget-function) 函数从数据库提取符合指定条件的单个记录。

- *database*：构成列表或数据库的单元格区域，包括标题。
- *field*：指定函数所使用的列名或索引。
- *criteria*：所指定条件的单元格区域，包括标题。

```sql
DGET(database, field, criteria)

# 获取指定条件的数据
=DGET($C$2:$E$2370, "访客数", N2:O3)
```

### 数据分析与处理

#### 排序

[`SORT`](https://support.microsoft.com/zh-cn/office/22f63bd0-ccc8-492f-953d-c20e8e44b86c) 对区域或数组的内容进行排序。

- *sort_order*：`1` 升序，`-1` 降序。默认为升序。
- *by_col*：`TRUE` 按列排序。`FALSE` 按行排序。默认为 `FALSE`。

```sql
SORT(array, [sort_index], [sort_order], [by_col])

# 按分数降序排列
=SORT(B5:C14, 2)
```

#### 逻辑检测

[`IF`](https://support.microsoft.com/zh-cn/office/69aed7c9-4e8a-4755-a9bc-aa8bbff73be2) 函数指定要执行的逻辑检测。

```sql
IF(logical_test, value_if_true, [value_if_false])

# 规避被除数为 0 的情况
=IF(B19=0, 0, B20/B19)
```

### 数据汇总与运算

#### 求和

[`SUM`](https://support.microsoft.com/zh-cn/office/043e1c7d-7726-4e80-8f32-07b23e057f89) 函数用于求参数的和。将单个值、单元格引用或区域相加，或者将三者的组合相加。

```sql
SUM(number1, [number2], ...)

=SUM(A1:A5)
```

[`SUMIFS`](https://support.microsoft.com/zh-cn/office/c9e748f5-7ea7-455d-9406-611cebce642b) 函数用于计算满足多个条件的全部参数的总量。

```sql
SUMIFS(sum_range, criteria_range1, criteria1, [criteria_range2, criteria2], ...)

# 获取指定条件的数据。此函数是汇总函数，但可匹配项多，可更好的替代 DGET 函数
=SUMIFS($E$3:$E$2370, $C$3:$C$2370, B$2, $D$3:$D$2370, "手淘搜索")

# 可以根据列汇总
=SUMIFS(E2:AH2, E1:AH1, ">="&DATE(2023, 6, 1), E1:AH1, "<="&DATE(2023, 6, 30))
```

#### 最大值

[`MAXIFS`](https://support.microsoft.com/zh-cn/office/dfd611e6-da2c-488a-919b-9b6376b28883) 函数返回一组给定条件或标准指定的单元格中的最大值。

```sql
MAXIFS(max_range, criteria_range1, criteria1, [criteria_range2, criteria2], ...)

# C 列为日期，E 列为访客数，获取指定日期的最大访客数
=MAXIFS(E:E, C:C, DATE(2023, 6, 1))
```

#### 单元格数量

[`COUNTA`](https://support.microsoft.com/zh-cn/office/7dc98875-d5c1-46f1-9a82-53f3219e2509) 函数计算单元格区域中不为空的单元格的个数。

```sql
COUNTA(value1, [value2], ...)

# 获取指定单元格区域中不为空的单元格个数
=COUNTA(B5:B15)

# 获取整列中不为空的单元格个数
=COUNTA(A:A)
```

[`SUBTOTAL`](https://support.microsoft.com/zh-cn/office/7b027003-f060-4ade-9040-e478765b9939) 函数返回列表或数据库中的分类汇总。

```sql
SUBTOTAL(function_num, ref1, [ref2], ...)

# 生成筛选后自动重排的序号
=SUBTOTAL(3, $B$2:B2)
```

### 日期与时间

#### 生成日期

[`DATE`](https://support.microsoft.com/zh-cn/office/e36c0c8c-4104-49da-ab83-82328b832349) 函数采用三个单独的值并将它们合并为一个日期。

```sql
DATE(year, month, day)

# 指定日期
=DATE(2022, 1, 1)
```

[`TODAY`](https://support.microsoft.com/zh-cn/office/5eb3078d-a82c-4736-8930-2f51a028fdd9) 函数返回今天日期的序列号。

```sql
=TODAY()
```

#### 提取日期

[`YEAR`](https://support.microsoft.com/zh-cn/office/c64f017a-1354-490d-981f-578e8ec8d3b9) 函数返回对应于某个日期的年份。

```sql
# 获取指定单元格引用的年份
=YEAR(A1)
```

[`MONTH`](https://support.microsoft.com/zh-cn/office/579a2881-199b-48b2-ab90-ddba0eba86e8) 函数返回日期中的月份。

```sql
# 获取指定单元格引用的月份
=MONTH(A1)
```

#### 比较日期

[`DATEDIF`](https://support.microsoft.com/zh-cn/office/25dba1a4-2812-480b-84dd-8b32a451b35c) 函数用于计算两个日期之间相隔的天数、[月数](https://support.microsoft.com/zh-cn/office/8235e7c9-b430-44ca-9425-46100a162f38#ID0EDDBBL)或年数。

```sql
DATEDIF(start_date, end_date, unit)

# 计算以月为单位的差值
=DATEDIF(B3, TODAY(), "m")
```

### 文本处理

#### 文本提取

[`LEFT`](https://support.microsoft.com/zh-cn/office/9203d2d2-7960-479b-84c6-1ea52b99640c) 函数从文本字符串的第一个字符开始返回指定个数的字符。

```sql
LEFT(text, [num_chars])

# 截取指定字符的前 15 个字符
=LEFT(I1, 15)
```

[`MID`](https://support.microsoft.com/zh-cn/office/2eba57be-0c05-4bdc-bf81-5ecf4421eb8a) 函数返回文本字符串中从指定位置开始的特定数目的字符。

```sql
MID(text, start_num, num_chars)

# 从字符串中第一个字符开始，返回五个字符
=MID("Fluid Flow", 1, 5)
```

[`FIND`](https://support.microsoft.com/zh-cn/office/c7912941-af2a-4bdf-a553-d0d89b0a0628) 函数用于在第二个文本串中定位第一个文本串，并返回第一个文本串的起始位置的值，该值从第二个文本串的第一个字符算起。

```sql
FIND(find_text, within_text, [start_num])

# 查找单元格 A2 中第一个 "M" 的位置，区分大小写
=FIND("M", A2)
```

[`SEARCH`](https://support.microsoft.com/zh-cn/office/9ab04538-0e55-4719-a72e-b6f54513b495) 函数可在第二个文本字符串中查找第一个文本字符串，并返回第一个文本字符串的起始位置的编号，该编号从第二个文本字符串的第一个字符算起。

```sql
SEARCH(find_text, within_text, [start_num])

# 查找对应字符串的位置，且不区分大小写
=SEARCH("apple", "I have an Apple")
```

#### 文本转换

[`CONCAT`](https://support.microsoft.com/zh-cn/office/9b1a9a3f-94ff-41af-9736-694cbd6b4ca2) 函数合并来自多个区域或字符串的文本，但它不提供分隔符参数。

```sql
CONCAT(text1, [text2], ...)
```

## 二、快捷键

使用 Excel 的[键盘快捷方式](https://support.microsoft.com/zh-cn/office/1798d9d5-842a-42b8-9c99-9b7213f0040f)可以提高工作效率。

| Command                                                      | Key                     | 分类           |
| ------------------------------------------------------------ | ----------------------- | -------------- |
| 移到工作表的开头                                             | `Ctrl + Home`           | 当前选定单元格 |
| 移到工作表的结尾                                             | `Ctrl + End`            | 当前选定单元格 |
| 移到当前数据区域边缘                                         | `Ctrl + 箭头键`         | 当前选定单元格 |
| 选择整行                                                     | `Shift + 空格键`        | 区域选择       |
| 选择整个表                                                   | `Ctrl + A`              | 区域选择       |
| [扩展单元格的选定范围](https://support.microsoft.com/zh-cn/office/1798d9d5-842a-42b8-9c99-9b7213f0040f#bkmk_selectwin) | `Ctrl + Shift + 箭头键` | 区域选择       |
| 填充颜色                                                     | `Alt + H + H`           | 样式           |
| [更改列宽以自动适合内容](https://support.microsoft.com/zh-cn/office/72f5e3cc-994d-43e8-ae58-9774a0905f46) | `Alt + H + O + I`       | 样式           |
| [打开或关闭筛选](https://support.microsoft.com/zh-cn/office/d6ae119c-5fab-4b7f-8869-6ef02ece71f5#bkmk_keyboardshortcuts_win) | `Ctrl + Shift + L`      | 筛选           |

## 三、样式

### 条件格式

根据公式确定要设置格式的单元格。

| 公式          | 应用于   | 描述     |
| ------------- | -------- | -------- |
| =`$H1="离职"` | =`$A:$H` | 整行变色 |

### 单元格格式

单元格格式功能非常强大，用于控制数据在单元格中的显示方式。

例如，用于显示数字，可以指定小数位数和千位分隔符。

```
#,##0
#,##0.00
```

可以通过具体限制数字的正负数、零、文本的显示格式，来[隐藏零值](https://support.microsoft.com/zh-cn/office/3ec7a433-46b8-4516-8085-a00e9e476b03)。

```python
0;-0;;@
```

## 四、公式

### 数组公式

[数组公式](https://support.microsoft.com/zh-cn/office/7d94a64e-3ff3-4686-9372-ecfd5caa57c7)是可以对数组中的一个或多个项执行多个计算的公式。它可以同时处理多个数据，并返回一个或多个结果。

数组公式通常用大括号 `{}` 包围，表示这是一个数组公式。不过，你不能直接输入大括号，而是需要通过按 `Ctrl + Shift + Enter` 来输入数组公式，Excel 会自动为你添加大括号。

具体步骤如下：

```
1、选择你希望显示结果的单元格区域（例如，B1:B6）。
2、输入公式 =A1:A6^2。
3、按 Ctrl + Shift + Enter，而不是仅按 Enter。
```

数组公式有两种主要类型：

- **单元格数组公式**：返回多个结果，并且这些结果会填充到多个单元格中。

  例如，可以使用数组公式来计算一列或一行中的每个元素的平方。

  ```sql
  =A1:A5^2
  ```

- **单元格内数组公式**：返回单个结果，即使它是基于多个值计算的。

  例如，可以使用数组公式来计算两个数组内对应元素的乘积之和。

  ```sql
  =SUM({1,2,3} * {4,5,6})
  ```

  例如，查找数组中的最大值。
  
    ```sql
    =MAX({1,2,3} * {4,5,6})
    ```

在 Excel 365 和 Excel 2019 中，微软引入了[动态数组](https://support.microsoft.com/zh-cn/office/205c6b06-03ba-4151-89a1-87a7eb36e531)功能，这使得数组公式的使用更加直观和简单，不再需要使用 `Ctrl + Shift + Enter`，可以直接输入公式并获得结果。在旧版 Excel 中打开包含动态数组公式的工作簿时，有一些[兼容性注意事项](https://support.microsoft.com/zh-cn/office/696e164e-306b-4282-ae9d-aa88f5502fa2)。

```sql
# 计算从指定范围内所有包含 "上班迟到" 的文本中提取的分钟数的总和
=SUM(
    IF(
        ISNUMBER(SEARCH("上班迟到", F10:AM10)),
        VALUE(
            MID(F10:AM10, SEARCH("上班迟到", F10:AM10) + 4,
                SEARCH("分钟", F10:AM10) - SEARCH("上班迟到", F10:AM10) - 4)
        ),
        0
    )
)

# 通过 IFERROR 函数，优化逻辑结构
=SUM(
    IFERROR(
        VALUE(
            MID(F10:AM10, SEARCH("上班迟到", F10:AM10) + 4,
                SEARCH("分钟", F10:AM10) - SEARCH("上班迟到", F10:AM10) - 4)
        ), 
        0
    )
)
```

## Reference

- [Exceljet](https://exceljet.net/)

